def versionNo = '0.2.4'

group 'com.github.excitement-engineer'
version versionNo

buildscript {
    ext.kotlin_version = '1.3.31'
    ext.ktor_version = '1.1.1'
    ext.spek_version = '2.0.5'

    repositories {
        mavenCentral()
        jcenter()

    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }

}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

dependencies {
    compile "io.ktor:ktor-server-core:$ktor_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.graphql-java:graphql-java:13.0"
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile "junit:junit:4.11"
    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java

            artifact sourcesJar

        }

    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    pkg {
        repo = 'ktor-graphql'
        name = 'ktor-graphql'
        licenses = ['MIT']
        vcsUrl = "https://github.com/excitement-engineer/ktor-graphql.git"
        publications = ['MyPublication']

        version {
            name = versionNo
            desc = 'GraphQL over HTTP for Ktor'
            released  = new Date()
            vcsTag = versionNo
        }
    }
}